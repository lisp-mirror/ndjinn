(in-package #:net.mfiano.lisp.pyx)

(defstruct (clock (:constructor %make-clock)
                  (:predicate nil)
                  (:copier nil))
  (accumulator 0d0)
  (delta-buffer 0d0)
  (fps/current 0d0)
  (fps/average 0d0)
  (fps/average/10s 0d0)
  (fps/average/30s 0d0)
  (fps/average/60s 0d0)
  (frame-count 0)
  (frame-time 0d0)
  (init-time 0)
  (alpha 0f0)
  (period-elapsed 0d0)
  (period-interval 0.25d0)
  (previous-time 0d0)
  (running-time 0d0))

(defun make-clock ()
  (let ((clock (%make-clock)))
    (setf (clock-init-time clock) (sb-ext:get-time-of-day)
          (clock-running-time clock) (get-time clock))
    (setf (clock) clock)))

(defun get-time (clock)
  (u:mvlet ((s us (sb-ext:get-time-of-day)))
    (+ (- s (clock-init-time clock))
       (/ us 1d6))))

(defun smooth-delta-time (clock refresh-rate)
  (symbol-macrolet ((frame-time (clock-frame-time clock)))
    (incf (clock-frame-time clock) (clock-delta-buffer clock))
    (let ((frame-count (max 1 (truncate (1+ (* frame-time refresh-rate)))))
          (previous frame-time))
      (setf frame-time (/ frame-count refresh-rate)
            (clock-delta-buffer clock) (- previous frame-time)))))

(defun calculate-frame-rate (clock)
  (let* ((time (clock-frame-time clock))
         (fps (/ time))
         (alpha10 (- 1 (exp (- (/ time 10)))))
         (alpha30 (- 1 (exp (- (/ time 30)))))
         (alpha60 (- 1 (exp (- (/ time 60)))))
         (frame-count (clock-frame-count clock)))
    (symbol-macrolet ((average/10s (clock-fps/average/10s clock))
                      (average/30s (clock-fps/average/30s clock))
                      (average/60s (clock-fps/average/60s clock))
                      (average (clock-fps/average clock)))
      (setf (clock-fps/current clock) fps)
      (if (> (clock-running-time clock) 3)
          (setf average/10s (+ (* alpha10 fps) (* (- 1 alpha10) average/10s))
                average/30s (+ (* alpha30 fps) (* (- 1 alpha30) average/30s))
                average/60s (+ (* alpha60 fps) (* (- 1 alpha60) average/60s))
                average (/ (+ fps (* (1- frame-count) average))
                           frame-count))
          (setf average/10s fps
                average/30s fps
                average/60s fps
                average fps)))))

(defun clock-update (clock func)
  (symbol-macrolet ((accumulator (clock-accumulator clock)))
    (incf accumulator (clock-frame-time clock))
    (when (zerop (clock-frame-count clock))
      (funcall func))
    (u:while (>= accumulator =delta-time=)
      (funcall func)
      (decf accumulator =delta-time=))
    (setf (clock-alpha clock) (float (/ accumulator =delta-time=) 1f0))))

(defun clock-update/periodic (clock func)
  (let ((current (clock-running-time clock)))
    (symbol-macrolet ((elapsed (clock-period-elapsed clock)))
      (when (>= (- current elapsed) (clock-period-interval clock))
        (funcall func)
        (setf elapsed current)))))

(defun tick-clock (clock refresh-rate update-func periodic-func)
  (let ((previous (clock-running-time clock))
        (current (get-time clock)))
    (setf (clock-previous-time clock) previous
          (clock-running-time clock) current
          (clock-frame-time clock) (- current previous))
    (when =vsync=
      (smooth-delta-time clock refresh-rate))
    (clock-update clock update-func)
    (clock-update/periodic clock periodic-func)
    (when (plusp (get-frame-count))
      (calculate-frame-rate clock))))

(defun get-alpha ()
  (clock-alpha (clock)))

(defun get-fps ()
  (let ((clock (clock)))
    (values (clock-fps/current clock)
            (clock-fps/average clock)
            (clock-fps/average/10s clock)
            (clock-fps/average/30s clock)
            (clock-fps/average/60s clock))))

(defun get-frame-count ()
  (clock-frame-count (clock)))

(defun get-frame-time ()
  (float (clock-frame-time (clock)) 1f0))

(defun get-running-time ()
  (clock-running-time (clock)))
