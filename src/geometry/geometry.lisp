(in-package #:net.mfiano.lisp.pyx)

(defun make-geometry (name)
  (let* ((geometry (funcall (u:href (metadata-geometry =metadata=) name)))
         (id (geometry-spec-id geometry)))
    (log:debug :pyx "Created geometry: ~s (VAO: ~d)" name id)
    geometry))

(defun %update-geometry (geometry buffer-name data)
  (let ((data (or data (make-array (geometry-spec-vertex-count geometry)
                                   :initial-element 0))))
    (fill-geometry-buffer geometry buffer-name data)
    (setf (geometry-spec-primitive-count geometry) (length data))
    (values)))

(defun draw-geometry (geometry instance-count)
  (gl:bind-vertex-array (geometry-spec-id geometry))
  (%gl:draw-arrays-instanced (geometry-spec-primitive geometry)
                             0
                             (* (geometry-spec-primitive-count geometry)
                                (geometry-spec-vertex-count geometry))
                             instance-count)
  (gl:bind-vertex-array 0)
  (values))

(defun delete-geometry (geometry)
  (let ((name (geometry-spec-name geometry))
        (id (geometry-spec-id geometry)))
    (gl:delete-buffers (geometry-spec-buffers geometry))
    (gl:delete-vertex-arrays (list id))
    (log:debug :pyx "Deleted geometry: ~s (VAO: ~d)" name id)))
